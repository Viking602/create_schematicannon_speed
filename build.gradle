plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.107'
}

version = mod_version
group = 'com.viking.lantudapao'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url = "https://maven.createmod.net" }
    maven { url = "https://maven.neoforged.net/releases" }
}

neoForge {
    version = neo_version

    runs {
        client {
            client()
        }
        server {
            server()
            programArgument '--nogui'
        }
    }

    mods {
        lantudapao {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    compileOnly "com.simibubi.create:create-${minecraft_version}:${create_version}:slim"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
        mod_id: mod_id,
        mod_name: mod_name,
        mod_version: mod_version,
        mod_authors: mod_authors,
        mod_description: mod_description,
        minecraft_version: minecraft_version,
        neo_version: neo_version,
        create_version: create_version,
        create_version_min: create_version_min,
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

jar {
    manifest {
        attributes([
            'MixinConfigs': 'lantudapao.mixins.json'
        ])
    }
}
